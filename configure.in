AC_PREREQ(2.50)
AC_INIT(libexif/exif-data.h)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(libexif, 0.5.7rc2)
AM_MAINTAINER_MODE

dnl ---------------------------------------------------------------------------
dnl Versioning:
dnl  - AGE (Micro):      Increment if any interfaces have been added; set to 0
dnl		         if any interfaces have been removed. Removal has 
dnl                      precedence over adding, so set to 0 if both happened.
dnl  - REVISION (Minor): Increment any time the source changes; set to 
dnl			 0 if you incremented CURRENT.
dnl  - CURRENT (Major):  Increment if the interface has additions, changes,
dnl                      removals.
dnl ---------------------------------------------------------------------------
LIBEXIF_AGE=0
LIBEXIF_REVISION=1
LIBEXIF_CURRENT=7
AC_SUBST(LIBEXIF_AGE)
AC_SUBST(LIBEXIF_REVISION)
AC_SUBST(LIBEXIF_CURRENT)
LIBEXIF_VERSION_INFO=`expr $LIBEXIF_CURRENT + $LIBEXIF_REVISION`:$LIBEXIF_AGE:$LIBEXIF_REVISION
AC_SUBST(LIBEXIF_VERSION_INFO)

AM_PROG_LIBTOOL

dnl ---------------------------------------------------------------------------
dnl i18n support
dnl ---------------------------------------------------------------------------
ALL_LINGUAS="de es"
# EVIL HACKS
AC_MSG_CHECKING([for po/Makevars requiring evil hack])
GETTEXT_PACKAGE="${PACKAGE}${LIBEXIF_CURRENT}"
AC_SUBST(GETTEXT_PACKAGE)
if po/Makevars.template
then
	sed -e "s/^DOMAIN.*/DOMAIN = ${GETTEXT_PACKAGE}/" < po/Makevars.template > po/Makevars
	AC_MSG_RESULT([yes. done.])
else
	AC_MSG_RESULT([no])
fi
AM_GNU_GETTEXT

dnl ---------------------------------------------------------------------------
dnl Warnings
dnl ---------------------------------------------------------------------------
if test "x$GCC" = "xyes"; then
    CFLAGS="$CFLAGS -g -Wall -Wmissing-declarations -Wmissing-prototypes"
    LDFLAGS="$LDFLAGS -g -Wall"
    AC_SUBST(CFLAGS)
    AC_SUBST(LDFLAGS)
fi

AC_OUTPUT([
  Makefile
  libexif.spec
  libexif/Makefile
  libexif/canon/Makefile
  libexif/fuji/Makefile
  libexif/hp/Makefile
  libexif/olympus/Makefile
  libjpeg/Makefile
  test/Makefile
  libexif/libexif.pc
])

echo "

Configuration (libexif):

	Source code location:  ${srcdir}
	Compiler:              ${CC}
"
