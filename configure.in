AC_PREREQ(2.59)
AC_INIT([EXIF library], [0.6.12], [libexif-devel@lists.sourceforge.net], [libexif])
AC_CONFIG_SRCDIR([libexif/exif-data.h])
AM_CONFIG_HEADER([config.h])
AM_INIT_AUTOMAKE([gnu 1.8 dist-bzip2])
AM_GNU_GETTEXT_VERSION([0.14.1])

dnl ---------------------------------------------------------------------------
dnl Versioning:
dnl  - AGE (Micro):      Increment if any interfaces have been added; set to 0
dnl		         if any interfaces have been removed. Removal has 
dnl                      precedence over adding, so set to 0 if both happened.
dnl  - REVISION (Minor): Increment any time the source changes; set to 
dnl			 0 if you incremented CURRENT.
dnl  - CURRENT (Major):  Increment if the interface has additions, changes,
dnl                      removals.
dnl ---------------------------------------------------------------------------
LIBEXIF_AGE=0
LIBEXIF_REVISION=0
LIBEXIF_CURRENT=12
AC_SUBST([LIBEXIF_AGE])
AC_SUBST([LIBEXIF_REVISION])
AC_SUBST([LIBEXIF_CURRENT])
LIBEXIF_VERSION_INFO=`expr $LIBEXIF_CURRENT + $LIBEXIF_REVISION`:$LIBEXIF_AGE:$LIBEXIF_REVISION
AC_SUBST([LIBEXIF_VERSION_INFO])

AM_PROG_LIBTOOL

dnl Create a stdint.h-like file containing size-specific integer definitions
dnl that will always be available
AC_NEED_STDINT_H([libexif/_stdint.h])


dnl ---------------------------------------------------------------------------
dnl i18n support
dnl ---------------------------------------------------------------------------
ALL_LINGUAS="de es fr"
# EVIL HACKS
AC_MSG_CHECKING([for po/Makevars requiring evil hack])
GETTEXT_PACKAGE=${PACKAGE}-${LIBEXIF_CURRENT}
AC_DEFINE_UNQUOTED([GETTEXT_PACKAGE],["$GETTEXT_PACKAGE"],[The gettext domain we're using])
AC_SUBST([GETTEXT_PACKAGE])
if test -f po/Makevars.template; then

sed -f - po/Makevars.template > po/Makevars <<-EOF
s/^DOMAIN.*/DOMAIN = ${GETTEXT_PACKAGE}/
s/^COPYRIGHT_HOLDER.*/COPYRIGHT_HOLDER = Lutz MÃ¼ller and others/
s/^MSGID_BUGS_ADDRESS.*/MSGID_BUGS_ADDRESS = libexif-devel@lists.sourceforge.net/
EOF
	AC_MSG_RESULT([yes. done.])
else
	AC_MSG_RESULT([no])
fi
AM_GNU_GETTEXT


dnl ---------------------------------------------------------------------------
dnl Warnings
dnl ---------------------------------------------------------------------------
if test "x$GCC" = "xyes"; then
    CFLAGS="$CFLAGS -g -Wall -Wmissing-declarations -Wmissing-prototypes"
    LDFLAGS="$LDFLAGS -g -Wall"
    AC_SUBST(CFLAGS)
    AC_SUBST(LDFLAGS)
fi

AC_CONFIG_FILES([ 
  intl/Makefile
  po/Makefile.in
  Makefile
  libexif.spec
  libexif/Makefile
  libexif/canon/Makefile
  libexif/olympus/Makefile
  libexif/pentax/Makefile
  libjpeg/Makefile
  test/Makefile
  m4/Makefile
  libexif/libexif.pc
])
AC_OUTPUT

echo "

Configuration (libexif):

	Source code location:  ${srcdir}
	Compiler:              ${CC}
	USE_NLS:               ${USE_NLS}
"
